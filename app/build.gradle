apply plugin: 'com.android.application'

android {
    namespace 'com.jaychang.demo.srv'
    compileSdk deps.build.compileSdkVersion
    defaultConfig {
        applicationId "com.jaychang.demo.srv"
        minSdkVersion deps.build.minSdkVersion
        targetSdkVersion deps.build.targetSdkVersion
        versionCode 4
        versionName "1.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation deps.support.appcompat
    implementation deps.support.swiperefreshlayout
    implementation deps.butterKnife.core
    annotationProcessor deps.butterKnife.compiler
    implementation project(':library')
    implementation project(':library-kotlin-android-extensions')
}

// ButterKnife annotation processor needs --add-opens on JDK 9+. Only add when JDK 9+ so JitPack (Java 8) is unchanged.
tasks.withType(JavaCompile).configureEach {
    if (JavaVersion.current().isJava9Compatible()) {
        options.fork = true
        options.forkOptions.jvmArgs.addAll([
            '--add-opens=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED',
            '--add-opens=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED',
            '--add-opens=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED'
        ])
    }
}
